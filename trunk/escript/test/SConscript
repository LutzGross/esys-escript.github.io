
########################################################
#
# Copyright (c) 2003-2010 by University of Queensland
# Earth Systems Science Computational Center (ESSCC)
# http://www.uq.edu.au/esscc
#
# Primary Business: Queensland, Australia
# Licensed under the Open Software License version 3.0
# http://www.opensource.org/licenses/osl-3.0.php
#
########################################################

import re
Import('*')
local_env = env.Clone()

if local_env['cppunit']:
    # get the test source file names
    sources = Glob('*.cpp')+Glob('*.c')
    testname='escript_UnitTest'

    # build the executable
    local_env.Append(LIBS=['escript', 'esysUtils']+env['cppunit_libs'])
    # some of these test files are too large to optimize
    local_env['CCFLAGS'] = re.sub('-O[0-9]', '-g', str(local_env['CCFLAGS']))
    program = local_env.Program(testname, sources)

    # run the tests - but only if test_targets are stale
    local_env.RunUnitTest(testname)

    # add unit test to target alias
    Alias('build_tests', program)
    Alias("run_tests", testname+'.passed')

    # add a group of tests
    from grouptest import *
    tgroup=GroupTest("$BINRUNNER ", (), "", "$BUILD_DIR/escript/test", ('./'+testname,))
    TestGroups.append(tgroup)

# configure python unit tests
local_env.SConscript(dirs = ['#/escript/test/python'], variant_dir='python', duplicate=0, exports=['py_wrapper_lib'])

