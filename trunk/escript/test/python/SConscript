Import('*')
local_env=env.Copy()
src_dir = local_env.Dir('.').srcnode().abspath
import os
filenames = os.listdir(src_dir)
#
# files defining tests but are not running by itself:
#
testfiles = [x for x in filenames if x.startswith('test_') and os.path.splitext(x)[1] in ['.py'] ]
# 
# directories defining test data
# 
testdata_dir = [x for x in filenames if x.startswith('data_') ]
# 
#  files defining test runs (passing in a release)
# 
testruns = [x for x in filenames if x.startswith('run_') and os.path.splitext(x)[1] in ['.py'] ]
#
# files defining tests run locally (not as part of a release)
#
localtestruns = [x for x in filenames if not x.startswith('run_') and os.path.splitext(x)[1] in ['.py']]
#
# all test 
#
alltestruns = testruns + localtestruns
#
# test files are just compiled:
#
test_pyc = env.PyCompile(testfiles)
env.Alias('build_py_tests', test_pyc)
#
# run all tests:
#
program = local_env.RunPyUnitTest(alltestruns)

Depends(program, pyinstall)
Depends(program, dep_lib) 
Depends(program, 'build_py_tests') 

#Add Unit Test to target alias
local_env.PrependENVPath('PYTHONPATH',str(env.Dir('#/build/$PLATFORM/escript/test/python')))
local_env.PrependENVPath('ESCRIPT_TEST_DATA_ROOT',str(env.Dir('#/escript/test/python')))
local_env.PrependENVPath('ESCRIPT_WORKDIR',str(env.Dir('#/build/$PLATFORM/escript/test/python')))
env.Alias('local_py_tests',[os.path.splitext(x)[0]+'.passed' for x in alltestruns])
env.Alias('py_tests', [os.path.splitext(x)[0]+'.passed' for x in testruns ])

# get all the python files in the release tar file
release_pyfiles = [ env.File("SConscript"), ]
release_testfiles = [ env.File(x) for x in testfiles + testruns ] + [ env.Dir(x,'#/escript/test/python') for x in testdata_dir ]
env.Zip(src_zipfile, release_pyfiles)
env.Tar(src_tarfile, release_pyfiles)
env.Zip(test_zipfile, release_testfiles)
env.Tar(test_tarfile, release_testfiles)

