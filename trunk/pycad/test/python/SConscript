
##############################################################################
#
# Copyright (c) 2003-2017 by The University of Queensland
# http://www.uq.edu.au
#
# Primary Business: Queensland, Australia
# Licensed under the Apache License, version 2.0
# http://www.apache.org/licenses/LICENSE-2.0
#
# Development until 2012 by Earth Systems Science Computational Center (ESSCC)
# Development 2012-2013 by School of Earth Sciences
# Development from 2014 by Centre for Geoscience Computing (GeoComp)
#
##############################################################################

from os.path import splitext
Import('*')

local_env = env.Clone()

# files defining test runs (passing in a release)
testruns = ['run_pycad_test.py']

# files defining tests run locally (not as part of a release)
localtestruns = [x for x in Glob('*.py', strings=True) if not x.startswith('run_')]

# all tests
alltestruns = testruns + localtestruns

# test files are just compiled - none of those in pycad yet
sources = Glob('run_*.py')
test_pyc = env.PyCompile(sources)
env.Alias('build_py_tests', test_pyc)

# add unit test to target alias
local_env.PrependENVPath('PYTHONPATH', Dir('#/pycad/test/python'))
local_env.PrependENVPath('PYTHONPATH', Dir('.').abspath)
local_env['ENV']['ESCRIPT_TEST_DATA_ROOT']=Dir('.').srcnode().abspath
local_env['ENV']['ESCRIPT_WORKDIR']=Dir('.').abspath
env.Alias('local_py_tests',[splitext(x)[0]+'.passed' for x in alltestruns])
env.Alias('py_tests', [splitext(x)[0]+'.passed' for x in testruns])

# run all tests
program = local_env.RunPyUnitTest(alltestruns)
Depends(program, 'build_py_tests')
if env['usempi']:
    Depends(program, env['prefix']+"/lib/pythonMPI")

# add a group of tests
from grouptest import GroupTest
tgroup=GroupTest("pycad", "$PYTHONRUNNER ", (("ESCRIPT_TEST_DATA_ROOT","$BATCH_ROOT/pycad/test/python"),("ESCRIPT_WORKDIR","$BUILD_DIR/pycad/test/python")), "$BATCH_ROOT/pycad/test/python", "$BATCH_ROOT/pycad/test/python", testruns)
tgroup.makeDir("$BUILD_DIR/pycad/test/python")
TestGroups.append(tgroup)

