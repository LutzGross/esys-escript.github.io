
########################################################
#
# Copyright (c) 2003-2010 by University of Queensland
# Earth Systems Science Computational Center (ESSCC)
# http://www.uq.edu.au/esscc
#
# Primary Business: Queensland, Australia
# Licensed under the Open Software License version 3.0
# http://www.opensource.org/licenses/osl-3.0.php
#
########################################################

import os
Import('*')
local_env = env.Clone(tools=['pdflatex'])

tex_src = Glob('*.tex')+Glob('*.bib')+Glob('*.sty')+Glob('*.cfg')+Glob('*.cls')
fig_src = Glob('figures/*.eps')

if 'PDF' in dir(local_env):
    # Convert eps figures to pdf
    figures=local_env.EpsToPDF(source=fig_src)

    if local_env['verbose']:
        interaction='-interaction=nonstopmode'
    else:
        interaction='-interaction=batchmode'

    # Define command to include SVN revision 
    local_env.Append(PDFLATEXFLAGS=[interaction,'\\\\newcommand{\\\\RepVersion}{%s\\\\xspace}\\\\input{guide.tex}'%(local_env['svn_revision'])])
    pdf=local_env.PDF(target='guide.pdf', source='guide.tex')
    Depends(pdf, figures+tex_src)

    pdf_path=os.path.join(release_dir, 'user', 'guide.pdf')
    pdf_inst=local_env.InstallAs(target=pdf_path, source=pdf)
    env.Alias('guide_pdf', pdf_inst)
else:
    env.Alias('guide_pdf', '', 'echo "PDF output not supported!"')
    env.AlwaysBuild('guide_pdf')

html_dir=os.path.join(release_dir,'user','html')
html=local_env.Command(Dir(html_dir), fig_src+tex_src,
                ["cd %s && latex2html -top_navigation \
                 -bottom_navigation \
                 -index_in_navigation \
                 -contents_in_navigation \
                 -next_page_in_navigation \
                 -previous_page_in_navigation \
                 -title \"esys users guide\" \
                 -noshow_section_numbers \
                 -dir %s -mkdir \
                 -address \"esys@esscc.uq.edu.au\" \
                 -antialias_text -antialias -transparent \
                 -noshort_extn \
                 -up_url \"https://launchpad.net/escript-finley\" \
                 -up_title \"escript\" guide.tex"%(Dir('.').abspath,html_dir)])

env.Alias('guide_html', html)
env.AlwaysBuild(Dir(html_dir))

