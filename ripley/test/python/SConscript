
##############################################################################
#
# Copyright (c) 2003-2018 by The University of Queensland
# http://www.uq.edu.au
#
# Primary Business: Queensland, Australia
# Licensed under the Apache License, version 2.0
# http://www.apache.org/licenses/LICENSE-2.0
#
# See CREDITS file for contributors and development history
#
##############################################################################

from os.path import splitext
Import('*')

local_env = env.Clone()

# files defining test runs (passing in a release)
testruns = Glob('run_*.py', strings=True)

# add unit test to target alias
local_env.PrependENVPath('PYTHONPATH', Dir('#/ripley/test/python/').abspath)
local_env.PrependENVPath('PYTHONPATH', Dir('.').abspath)
local_env.PrependENVPath('PYTHONPATH', env.Dir('$BUILD_DIR/$PLATFORM/escriptcore/test/python').abspath)
local_env['ENV']['RIPLEY_TEST_DATA'] = Dir('.').srcnode().abspath
local_env['ENV']['RIPLEY_WORKDIR'] = Dir('.').abspath
# needed for a test from the util base class in escript
local_env['ENV']['ESCRIPT_WORKDIR'] = Dir('.').abspath
env.Alias('local_py_tests',[splitext(x)[0]+'.passed' for x in testruns])
env.Alias('py_tests', [splitext(x)[0]+'.passed' for x in testruns])

# run all tests
program = local_env.RunPyUnitTest(testruns)
Requires(program, ['install_escript', 'build_py_tests'])
if env['usempi']:
    Requires(program, ['install_pythonMPI'])

# Add a group of tests
from grouptest import GroupTest
tgroup=GroupTest("ripley", "$PYTHONRUNNER ", (("RIPLEY_TEST_DATA","$BATCH_ROOT/ripley/test/python"),('RIPLEY_WORKDIR','$BUILD_DIR/ripley/test/python')), "$BATCH_ROOT/escriptcore/test/python:$BATCH_ROOT/ripley/test/python", "$BATCH_ROOT/ripley/test/python", testruns)
tgroup.makeDir("$BUILD_DIR/ripley/test/python")
TestGroups.append(tgroup)

