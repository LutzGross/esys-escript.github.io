
##############################################################################
#
# Copyright (c) 2003-2020 by The University of Queensland
# http://www.uq.edu.au
#
# Primary Business: Queensland, Australia
# Licensed under the Apache License, version 2.0
# http://www.apache.org/licenses/LICENSE-2.0
#
# See CREDITS file for contributors and development history
# Development from 2019 by School of Earth and Environmental Sciences
#
##############################################################################


Import('*')
local_env = env.Clone()

if local_env['cppunit']:
    # get the test source file names
    sources = Glob('*.cpp')
    testname='weipa_UnitTest'

    # build the executable
    local_env.AppendUnique(LIBS=env['weipa_libs']+env['cppunit_libs'])
    if 'finley' in env['domains']:
        local_env.Append(CPPDEFINES = ['USE_FINLEY'])
    if 'ripley' in env['domains']:
        local_env.Append(CPPDEFINES = ['USE_RIPLEY'])
    if 'oxley' in env['domains']:
        local_env.Append(CPPDEFINES = ['USE_OXLEY'])
    if 'speckley' in env['domains']:
        local_env.Append(CPPDEFINES = ['USE_SPECKLEY'])
    program = local_env.Program(testname, sources)

    # run the tests - but only if test_targets are stale
    local_env.RunUnitTest(testname)

    # add unit test to target alias
    Alias('build_tests', program)
    Alias("run_tests", testname+'.passed')

    # add a group of tests
    from grouptest import GroupTest
    tgroup=GroupTest("weipacpp", "$BINRUNNER ", (), "", "$BUILD_DIR/weipa/test", ('./'+testname,))
    TestGroups.append(tgroup)

# configure python unit tests
local_env.SConscript('python/SConscript', duplicate=0)

