
##############################################################################
#
# Copyright (c) 2003-2015 by University of Queensland
# http://www.uq.edu.au
#
# Primary Business: Queensland, Australia
# Licensed under the Open Software License version 3.0
# http://www.opensource.org/licenses/osl-3.0.php
#
# Development until 2012 by Earth Systems Science Computational Center (ESSCC)
# Development 2012-2013 by School of Earth Sciences
# Development from 2014 by Centre for Geoscience Computing (GeoComp)
#
##############################################################################

from os.path import splitext
Import('*')

local_env = env.Clone()

# files defining test runs (passing in a release)
testruns = []
testruns += ['run_savevtk_tests.py']
testruns += ['run_savesilo_tests.py']

# files defining tests run locally (not as part of a release)
localtestruns = [x for x in Glob('*.py', strings=True) if not x.startswith('run_')]

# all tests
alltestruns = testruns + localtestruns

# test files are just compiled
test_pyc = env.PyCompile(alltestruns)
env.Alias('build_py_tests', test_pyc)

# add unit test to target alias
local_env['ENV']['WEIPA_TEST_MESHES']=env.Dir('#/weipa/test/python').abspath
local_env.PrependENVPath('PYTHONPATH', Dir('.').abspath)
local_env.PrependENVPath('PYTHONPATH', env.Dir('$BUILD_DIR/$PLATFORM/weipa/test/python').abspath)
local_env['ENV']['WEIPA_TEST_MESHES']=env.Dir('#/weipa/test/python/meshes').abspath
local_env['ENV']['WEIPA_WORKDIR']=Dir('.').abspath
env.Alias('local_py_tests',[splitext(x)[0]+'.passed' for x in alltestruns])
env.Alias('py_tests', [splitext(x)[0]+'.passed' for x in testruns])

# run all tests
program = local_env.RunPyUnitTest(alltestruns)
Depends(program, 'build_py_tests')
if env['usempi']:
    Requires(program, '#lib/pythonMPI')

# add a group of tests
from grouptest import *
tgroup=GroupTest("$PYTHONRUNNER ",(("WEIPA_TEST_DATA","$BATCH_ROOT/weipa/test/python"),('WEIPA_WORKDIR','$BUILD_DIR/weipa/test/python')),"$BATCH_ROOT/escriptcore/test/python:$BATCH_ROOT/weipa/test/python","$BATCH_ROOT/weipa/test/python",testruns)
tgroup.makeDir("$BUILD_DIR/weipa/test/python")
TestGroups.append(tgroup)

