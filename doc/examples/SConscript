
##############################################################################
#
# Copyright (c) 2003-2026 by the esys.escript Group
# http://www.uq.edu.au
#
# Primary Business: Queensland, Australia
# Licensed under the Apache License, version 2.0
# http://www.apache.org/licenses/LICENSE-2.0
#
# See CREDITS file for contributors and development history
#
##############################################################################

from grouptest import GroupTest
import os
Import('*')
local_env = env.Clone()

example_files = example_files_allow_mpi + example_files_no_mpi

src_dir = local_env.Dir('.').srcnode().abspath
test_dir= Dir('test', local_env.Dir('.'))
cc=Command(test_dir, [], Mkdir("$TARGET"))

# create copy of all non-py files:
# We need this because of cblib.py which needs to be present for other tests
# to work
data_files=[]
for i in example_deps:
    f_in=File(os.path.join(src_dir,i))
    f_out=File(i,test_dir)
    data_files.append(Command(f_out, f_in, Copy("$TARGET", "$SOURCE")))

programs=[]
copies=[]
for i in example_files:
    if local_env['mpi']=='none' or not i in example_files_no_mpi:
        f_in=File(os.path.join(src_dir,i))
        f_out=File(i, test_dir)
        c=Command(f_out, f_in, Copy("$TARGET", "$SOURCE"))
        copies+=c
        programs+=local_env.RunPyExample(c)

env.Alias('build_py_tests', data_files)
env.Alias('build_py_tests', copies)
env.Alias('py_tests', programs)
Depends(programs, data_files)
Depends(cc, data_files)

# Add group of tests
dirs = set([os.path.split(i)[0] for i in example_files])
for d in dirs:
    single_runs=[]
    runs=[]
    runs_metadata={}
    single_runs_metadata={}
    for i in example_files_no_mpi:
        if i.startswith(d):
            basename = os.path.split(i)[1]
            single_runs.append(basename)
            if i in example_metadata:
                single_runs_metadata[basename] = example_metadata[i]
    for i in example_files_allow_mpi:
        if i.startswith(d):
            basename = os.path.split(i)[1]
            runs.append(basename)
            if i in example_metadata:
                runs_metadata[basename] = example_metadata[i]
    TestGroups.append(GroupTest(d, "$PYTHONRUNNER ", (), "", os.path.join("$BATCH_ROOT/doc/examples",d),
                                runs, single_runs, runs_metadata, single_runs_metadata))

