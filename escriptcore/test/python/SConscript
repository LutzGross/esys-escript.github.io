
##############################################################################
#
# Copyright (c) 2003-2014 by University of Queensland
# http://www.uq.edu.au
#
# Primary Business: Queensland, Australia
# Licensed under the Open Software License version 3.0
# http://www.opensource.org/licenses/osl-3.0.php
#
# Development until 2012 by Earth Systems Science Computational Center (ESSCC)
# Development 2012-2013 by School of Earth Sciences
# Development from 2014 by Centre for Geoscience Computing (GeoComp)
#
##############################################################################

from os.path import splitext
Import('*')

local_env = env.Clone()

#  files defining test runs (passing in a release)
testruns = []
testruns += ['run_xml.py']
testruns += ['run_units.py']
testruns += ['run_data_access.py']
testruns += ['run_testdomain.py']
if env['sympy']:
    testruns += ['run_symbolic.py']


# files defining tests run locally (not as part of a release)
localtestruns = [x for x in Glob('*.py', strings=True) if not x.startswith('run_')]

# all test
alltestruns = testruns + localtestruns

# test files are just compiled
sources = Glob('test_*.py')
test_pyc = env.PyCompile(sources)
env.Alias('build_py_tests', test_pyc)

# add unit test to target alias
local_env.PrependENVPath('PYTHONPATH', Dir('.').abspath)
local_env.PrependENVPath('PYTHONPATH', '.')
local_env['ENV']['ESCRIPT_TEST_DATA_ROOT']=Dir('.').srcnode().abspath
local_env['ENV']['ESCRIPT_WORKDIR']=Dir('.').abspath
env.Alias('local_py_tests',[splitext(x)[0]+'.passed' for x in alltestruns])
env.Alias('py_tests', [splitext(x)[0]+'.passed' for x in testruns])

# run all tests
program = local_env.RunPyUnitTest(alltestruns)
Depends(program, [py_wrapper_lib, 'install_escript_py', 'build_py_tests'])

# add a group of tests
from grouptest import *
tgroup=GroupTest("$PYTHONRUNNER ",(("ESCRIPT_TEST_DATA_ROOT","$BATCH_ROOT/escriptcore/test/python"),('ESCRIPT_WORKDIR','$BUILD_DIR/escriptcore/test/python')),"$BATCH_ROOT/escriptcore/test/python","$BATCH_ROOT/escriptcore/test/python",testruns)
TestGroups.append(tgroup)

