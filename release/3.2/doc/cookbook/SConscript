
########################################################
#
# Copyright (c) 2009-2010 by University of Queensland
# Earth Systems Science Computational Center (ESSCC)
# http://www.uq.edu.au/esscc
#
# Primary Business: Queensland, Australia
# Licensed under the Open Software License version 3.0
# http://www.opensource.org/licenses/osl-3.0.php
#
########################################################

import os
Import('*')
local_env = env.Clone(tools=['pdflatex'])

tex_src = Glob('*.tex')+Glob('*.bib')+Glob('*.cls')
fig_src = Glob('figures/*.png')+Glob('figures/*.pdf')




if 'PDF' in dir(local_env):
    if local_env['verbose']:
        interaction='-interaction=nonstopmode'
    else:
        interaction='-interaction=batchmode'

    # Define command to include SVN revision
    local_env.Append(PDFLATEXFLAGS=[interaction,'\\\\newcommand{\\\\RepVersion}{%s\\\\xspace}\\\\input{cookbook.tex}'%(local_env['svn_revision'])])
    cbpdf=local_env.PDF(target='cookbook.pdf', source='cookbook.tex')
    Depends(cbpdf, fig_src+tex_src)

    pdf_path=os.path.join(release_dir, 'cookbook', 'cookbook.pdf')
    pdf_inst=local_env.InstallAs(target=pdf_path, source='cookbook.pdf')
    env.Alias('cookbook_pdf', pdf_inst)
else:
    env.Alias('cookbook_pdf', '', 'echo "PDF output not supported!"')
    env.AlwaysBuild('cookbook_pdf')

