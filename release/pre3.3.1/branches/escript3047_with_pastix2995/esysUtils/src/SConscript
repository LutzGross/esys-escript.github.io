
########################################################
#
# Copyright (c) 2003-2010 by University of Queensland
# Earth Systems Science Computational Center (ESSCC)
# http://www.uq.edu.au/esscc
#
# Primary Business: Queensland, Australia
# Licensed under the Open Software License version 3.0
# http://www.opensource.org/licenses/osl-3.0.php
#
########################################################


import os
Import('*')

local_env = clone_env(env_mpi)

sources = """
	EsysAssertException.cpp
	EsysException.cpp
	esysExceptionTranslator.cpp
        blocktimer.c
""".split()
headers = """
	EsysAssert.h
	EsysAssertException.h
	EsysException.h
	EsysTypes.h
	esys_malloc.h
	esysExceptionTranslator.h
        blocktimer.h
	system_dep.h
""".split()

lib_name = 'esysUtils'

if IS_WINDOWS_PLATFORM :
  local_env.Append(CPPDEFINES = ['ESYSUTILS_EXPORTS'])

if local_env['share_esysUtils'] :
  lib = local_env.SharedLibrary(lib_name, sources)
else:
  lib = local_env.StaticLibrary(lib_name, sources)

env.Alias('target_esysUtils_a', lib)

include_path = Dir(lib_name, local_env['incinstall'])

tmp1 = local_env.Install(include_path, headers )
env.Alias('target_install_esysUtils_headers', [tmp1])

tmp3 = local_env.Install(local_env['libinstall'], lib)
env.Alias('target_install_esysUtils_a', tmp3)

# Call the unit tests SConscript 
# export the lib target since tests will depend on it
# the lib target is a list of file nodes (why? win32 produces more than one output file: .lib, .dll, .pdb)
# FIXME: This list handling produces the desired result but can this be done directly with scons File nodes?

dep_lib = [local_env['libinstall']+'/'+str(x) for x in lib]
Export('dep_lib')

local_env.SConscript(dirs = ['#/esysUtils/test'], build_dir='#/build/$PLATFORM/esysUtils/test', duplicate=0)

