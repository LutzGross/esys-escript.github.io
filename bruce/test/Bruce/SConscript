# Scons configuration file for Bruce unit tests

import os
import sys

#
# ensure correct versions of python and scons

EnsurePythonVersion(2,3)
EnsureSConsVersion(0,96)

#
# set appropriate defaults for configuration variables

esysroot = Dir('#../../..')
usegcc = 0
options = None
sys_libs = []

#
# import configuration variables passed in from
# calling SConscript (if any)

Import('*')

#
# retreive command-line arguments if any

if ARGUMENTS.get('usegcc',0):
  usegcc = 1

if ARGUMENTS.get('options',0):
  options = ARGUMENTS.get('options',0)

#
# determine platform

env = Environment(ENV = os.environ)
platform = env['PLATFORM']

#
# determine hostname

import socket
hostname = socket.gethostname()

#
# load configuration settings

options_dir = str(esysroot) + '/scons'
sys.path.append(options_dir)

from default_options import *

if hostname=='ess':
  from ess_options import *

if usegcc==1:
  from gcc_options import *

if options!=None:
  exec "from " + options + " import *"

cxx_flags=cxx_flags_debug

#
# print out build configuration for this module

print "Build configuration for module: Bruce unit tests"
print "         dodebug:  1"
print "          usegcc: ", usegcc
print "             cxx: ", cxx
print "        platform: ", platform
print "        hostname: ", hostname

#
# do the actual build

bruce_path = str(esysroot) + '/bruce/inc'
bruce_test_path = str(esysroot) + '/bruce/test/Bruce'
escript_path = str(esysroot) + '/escript/inc'
esysUtils_path = str(esysroot) + '/esysUtils/inc'
CppUnitTest_path = str(esysroot) + '/tools/CppUnitTest/inc'

esys_lib_path = str(esysroot) + '/lib'
esysUtils_lib_path = str(esysroot) + '/esysUtils/lib'
CppUnitTest_lib_path = str(esysroot) + '/tools/CppUnitTest/lib'

bruce_lib = str(esys_lib_path) + '/libbrucecpp.so'

install_dir = str(esysroot) + '/bruce/test'

cpp_path = [bruce_path,
            bruce_test_path,
            escript_path,
            esysUtils_path,
            CppUnitTest_path,
            python_path,
            boost_path]

lib_path = [esys_lib_path,
            esysUtils_lib_path,
            CppUnitTest_lib_path,
            python_lib_path,
            boost_lib_path]

libs = ['brucecpp',
        'escriptcpp',
        'esysUtils',
        'CppUnitTest',
        str(python_lib),
        str(boost_lib),
        'dl',
        'util']

libs.extend(sys_libs)

sources = ['BruceTestCase.cpp',
           'BruceTest.cpp']

target = 'BruceTest.exe'

bruce_env = Environment(ENV = os.environ)

bruce_env.Replace(CXX = cxx)
bruce_env.Replace(CXXFLAGS = cxx_flags)
bruce_env.Replace(CPPPATH = cpp_path)
bruce_env.Replace(LIBPATH = lib_path)
bruce_env.Replace(LIBS = libs)

bruce_test_exe = bruce_env.Program(target, sources)

Depends(bruce_test_exe, bruce_lib)

Alias("build_tests", bruce_env.Install(install_dir, bruce_test_exe))

import scons_extensions
runUnitTest_builder = Builder(action = scons_extensions.runUnitTest, suffix = '.passed', single_source=True)
bruce_env.Append(BUILDERS = {'RunUnitTest' : runUnitTest_builder});

Alias("run_tests", bruce_env.RunUnitTest(target))
